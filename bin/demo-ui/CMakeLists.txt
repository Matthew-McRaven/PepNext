cmake_minimum_required(VERSION 3.23)
project(pep10 VERSION 0.2 LANGUAGES CXX)

# pick the C++ standard to use, in this case C++20
set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

#tell CMake to run the Qt tools moc, rcc, and uic automatically
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)
set(CMAKE_AUTOUIC ON)

find_package(Qt6 6.6 COMPONENTS Core REQUIRED)
find_package(Qt6 6.6 COMPONENTS Gui REQUIRED)
find_package(Qt6 6.6 COMPONENTS Qml REQUIRED)
find_package(Qt6 6.6 COMPONENTS Quick REQUIRED)
find_package(Qt6 6.6 COMPONENTS QuickControls2 REQUIRED)
find_package(Qt6 6.6 COMPONENTS Widgets REQUIRED)

if (WIN32)
#! [appicon_windows]
    set(app_icon_resource_windows "${CMAKE_CURRENT_SOURCE_DIR}/resources/pep10.rc")
    qt_add_executable(pep10 main.cpp ${app_icon_resource_windows})
#! [appicon_windows]
elseif (APPLE)
#! [appicon_macOS]
    # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
    # generated by CMake. This variable contains the .icns file name,
    # without the path.
    set(MACOSX_BUNDLE_ICON_FILE pep10.icns)

    # And the following tells CMake where to find and install the file itself.
    set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/resources/pep10.icns")
    set_source_files_properties(${app_icon_macos} PROPERTIES
           MACOSX_PACKAGE_LOCATION "Resources")

    qt_add_executable(pep10 MACOSX_BUNDLE main.cpp ${app_icon_macos})
#! [appicon_macOS]
else()
    qt_add_executable(pep10 main.cpp)
endif()

# GLOB_RECURSE uses abs paths, QML_FILES expects relative paths. This blob converts from abs to rel.
file(GLOB_RECURSE abs_qml_sources CONFIGURE_DEPENDS  "main.qml" "./ui/*.qml")
SET(rel_qml_sources, "")
foreach(f_abs IN LISTS abs_qml_sources)
    file(RELATIVE_PATH f_rel "${CMAKE_CURRENT_LIST_DIR}" ${f_abs})
    list(APPEND rel_qml_sources "${f_rel}")
endforeach()

qt_add_qml_module(pep10
    URI Pep10
    VERSION 0.2
    QML_FILES ${rel_qml_sources}
    SOURCES model/memorymodel.h model/memorymodel.cpp
    SOURCES model/statusbitmodel.h model/statusbitmodel.cpp
    SOURCES model/registermodel.h model/registermodel.cpp
    SOURCES model/memorybytemodel.h model/memorybytemodel.cpp
)
set_target_properties(pep10 PROPERTIES FOLDER "qtc_runnable")
target_link_libraries(pep10 PUBLIC
    Qt::Core
    Qt::Gui
    Qt::Qml
    Qt::Quick
    Qt::QuickControls2
    Qt::Widgets
)

# Resources:
set(pep10_resource_files
#    "main.qml"
    "resources/logo.png"
)

qt6_add_resources(pep10 "pep10"
    PREFIX
        "/"
    FILES
        ${pep10_resource_files}
)

install(TARGETS pep10
    RUNTIME DESTINATION "${INSTALL_EXAMPLEDIR}"
    BUNDLE DESTINATION "${INSTALL_EXAMPLEDIR}"
    LIBRARY DESTINATION "${CMAKE_INSTALL_LIBDIR}"
)
